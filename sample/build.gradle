import java.security.MessageDigest

apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.xblock.sample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(":xblocks")
}

task zipAssets(type: Zip) {
    from('src/main') {
        include 'assets/**'
    }
    destinationDir = file('src/main/assets')
    exclude 'assets/assets.zip'
    archiveName 'assets.zip'
}

task setZipHash << {
    ext.zipHash = "\"" + getZipHash() + "\""
    android.buildTypes.debug {
        buildConfigField "String", "ASSETS_ZIP_HASH", zipHash
    }
}

preBuild.dependsOn setZipHash
setZipHash.dependsOn zipAssets

def getZipHash() {
    MessageDigest sha1 = MessageDigest.getInstance("SHA-1");
    String fileContents = file('src/main/assets/assets.zip').text
    sha1.update(fileContents.bytes)
    Formatter hexHash = new Formatter()
    sha1.digest().each { b -> hexHash.format('%02x', b) }
    return hexHash
}